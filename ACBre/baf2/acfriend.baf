IF 
	InParty(Myself)
	PartyHasItem("ACHAND1")
	Global("ACBREFRIENDACTIVE","GLOBAL",0)
	!Global("ENDOFBG1","GLOBAL",2)
THEN
	RESPONSE #100
		RealSetGlobalTimer("ACBREFRIENDTALKTIMER","GLOBAL",300)
		SetGlobal("ACBREFRIENDACTIVE","GLOBAL",1)
		SetGlobal("ACBREBANTER1","LOCALS",1)
END

IF 
	InParty(Myself)
	PartyHasItem("ACHAND3")
	!Global("ENDOFBG1","GLOBAL",2)
	Global("ACBREBANTER2","LOCALS",0)
	Global("ACBREFRIENDACTIVE","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("ACBREBANTER2","LOCALS",1)
END

IF 
	InParty(Myself)
	Global("CHAPTER","GLOBAL",5)
	Global("ACBREBANTER1","LOCALS",2)
	Global("ACSPEEDMEUP","LOCALS",0)
	Global("ACBREFRIENDACTIVE","GLOBAL",1)
	!RealGlobalTimerExpired("ACBREFRIENDTALKTIMER","GLOBAL")
	!Global("ENDOFBG1","GLOBAL",2)
THEN
	RESPONSE #100
		RealSetGlobalTimer("ACBREFRIENDTALKTIMER","GLOBAL",10)
		SetGlobal("ACSPEEDMEUP","LOCALS",1)
END

IF
	InParty(Myself)
	See(Player1)
	RealGlobalTimerExpired("ACBREFRIENDTALKTIMER","GLOBAL")
	Global("ACBREFRIENDACTIVE","GLOBAL",1)
	CombatCounter(0)
THEN
	RESPONSE #100
		IncrementGlobal("ACBREFRIENDACTIVE","GLOBAL",1)
END

IF 
	InParty(Myself)
	GlobalGT("ACGROTHIQUEST","GLOBAL",1)
	Global("ACBREFRIENDACTIVE","GLOBAL",1)
	!Global("ENDOFBG1","GLOBAL",2)
	Global("ACBREBANTER3","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("ACBREBANTER3","LOCALS",1)
END

IF
	InParty(Myself)
	Dead("DeathK")
	AreaCheck("ARD016")	
	!Global("ENDOFBG1","GLOBAL",2)
	Global("ACBREBANTER4","LOCALS",0)
	Global("ACBREFRIENDACTIVE","GLOBAL",1)
	!StateCheck("ACBRE",CD_STATE_NOTVALID)
THEN
	RESPONSE #100
		SetGlobal("ACBREBANTER4","LOCALS",1)
END

IF 
	InParty(Myself)
	Dead("DeathK")
	AreaType(OUTDOOR)
	!Global("ENDOFBG1","GLOBAL",2)
	Global("ACBREBANTER4","LOCALS",1)
	Global("ACBREFRIENDACTIVE","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("ACBREBANTER4","LOCALS",2)
END

IF 
	InParty(Myself)
	!AreaCheck("AR0601")
	!AreaCheck("AR0602")
	!AreaCheck("AR0603")
	Global("ENDOFBG1","GLOBAL",2)
	Global("ACBREBANTER5","LOCALS",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("ACBREFRIENDTALKTIMER","GLOBAL",1)
		SetGlobal("ACBREFRIENDACTIVE","GLOBAL",1)
		SetGlobal("ACBREBANTER5","LOCALS",1)
END

IF
	InParty(Myself) 
	See(Player1)
	Global("ACBREFRIENDACTIVE","GLOBAL",2)
	RealGlobalTimerExpired("ACBREFRIENDTALKTIMER","GLOBAL")
	!StateCheck("ACBRE",CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
OR(9)
	Global("ACBREBANTER1","LOCALS",1)
	Global("ACBREBANTER1","LOCALS",2)
	Global("ACBREBANTER1","LOCALS",4)
	Global("ACBREBANTER2","LOCALS",1)
	Global("ACBREBANTER2","LOCALS",2)
	Global("ACBREBANTER3","LOCALS",1)
	Global("ACBREBANTER3","LOCALS",2)
	Global("ACBREBANTER4","LOCALS",2)
	Global("ACBREBANTER5","LOCALS",1)
//	Global("ACBREBANTER5","LOCALS",2)
THEN
	RESPONSE #100
		PlaySong(999991)
		StartDialogNoSet(Player1)
END

IF
	InParty(Myself)
	See(Player1)
	RealGlobalTimerExpired("ACBREFRIENDTALKTIMER","GLOBAL")
	Global("ACBREFRIENDACTIVE","GLOBAL",2)
	CombatCounter(0)
	!Global("ACBREBANTER1","LOCALS",1)
	!Global("ACBREBANTER1","LOCALS",2)
	!Global("ACBREBANTER1","LOCALS",4)
	!Global("ACBREBANTER2","LOCALS",1)
	!Global("ACBREBANTER2","LOCALS",2)
	!Global("ACBREBANTER3","LOCALS",1)
	!Global("ACBREBANTER3","LOCALS",2)
	!Global("ACBREBANTER4","LOCALS",2)
	!Global("ACBREBANTER5","LOCALS",1)
THEN
	RESPONSE #100
		RealSetGlobalTimer("ACBREFRIENDTALKTIMER","GLOBAL",ACBRE_TIMER)
		SetGlobal("ACBREFRIENDACTIVE","GLOBAL",1)
END